WHITESPACE = _{ " " | "\n" | "\t" | "\0" }
COMMENT    = _{ "#" ~ (!("\n") ~ ANY)* }

sym_chars = _{ ASCII_ALPHANUMERIC | "!" | "$" | "%" | "&" | "*" | "+" | "-" | "." | ":" | "<" | "?" | "=" | ">" | "@" | "^" | "_" }

token    = @{ sym_chars+ }
symbol   = ${ (token ~ "/")? ~ token+ }
keyword  = ${ ":" ~ token }
constant =  { "true" | "false" | "nil" }

string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | "0")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
plist   = { "(" ~ root ~ ")" }
blist   = { "[" ~ root ~ "]" }

value = _{ constant | number | keyword | string | special | plist | blist | symbol }

root = _{ value* }

special = { if_ | defn | fn_ | set | var }
if_  = { "(" ~ "if" ~ #cond = value ~ #then = value ~ #else = value? ~ ")" }
defn = { "(" ~ "defn" ~ #name = token ~ #args = arguments ~ #body = value+ ~ ")" }

fn_  = { "(" ~ "fn" ~ #args = arguments ~ #body = value+ ~ ")"}
arguments = { "[" ~ token* ~ "]" }

set  = { "(" ~ "set" ~ #name = token ~ value ~ ")" }
var  = { "(" ~ "var" ~ #name = token ~ value ~ ")" }

// TODO? Separate `var` and `def`?

main = { SOI ~ root ~ EOI }
