WHITESPACE = _{ " " | "\n" | "\t" | "\0" }
COMMENT    = _{ "#" ~ (!("\n") ~ ANY)* }

sym_chars = _{ ASCII_ALPHANUMERIC | "!" | "$" | "%" | "*" | "+" | "-" | "." | "<" | "?" | "=" | ">" | "@" | "^" | "_" }

token    = @{ sym_chars+ }
symbol   = ${ (token ~ "/")? ~ token+ }
keyword  = ${ ":" ~ token }
constant =  { "true" | "false" | "nil" }

string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | "0")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
plist   = { "(" ~ root ~ ")" }
blist   = { "[" ~ root ~ "]" }

value = _{ constant | number | keyword | string | special | plist | blist | symbol }

root = _{ value* }
special = { defn | fn_ | if_ | import | set | var }

parameter = ${ token ~ (":" ~ type)? }
type = @{ "number" | "string" | "list" | "module" }

parameters = { parameter* }
optional_params = { "&opt" ~ parameter+ }
rest_param = { "&" ~ parameter }

function_parameters = { "[" ~ parameters ~ (optional_params)? ~ (rest_param)? ~ "]" }

function_body = { value +}
defn   = { "(" ~ "defn" ~ #name = token ~ function_parameters ~ function_body ~ ")" }
fn_    = { "(" ~ "fn" ~ function_parameters ~ function_body ~ ")"}

if_    = { "(" ~ "if" ~ #cond = value ~ #then = value ~ #else = value? ~ ")" }
import = { "(" ~ "import" ~ #name = token ~ (":as" ~ #prefix = string)? ~ ")" }
set    = { "(" ~ "set" ~ #name = token ~ value ~ ")" }
var    = { "(" ~ "var" ~ #name = token ~ value ~ ")" }

// TODO? Separate `var` and `def`?

main = { SOI ~ root ~ EOI }
